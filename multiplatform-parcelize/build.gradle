plugins {
    id("com.android.library")
    id 'org.jetbrains.kotlin.multiplatform'
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    androidExtensions {
        experimental = true
    }

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Android Gradle Plugin expects sources to be in main, test, and androidTest
    // In order to keep our code structure consistent across platforms, we redefine
    // the sourceset directories here.
    sourceSets {
        // Main code is in androidMain
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
        // Unit test code is in androidTest (parallels jvmTest, jsTest)
        test {
            java.srcDirs = ['src/androidTest/kotlin']
            res.srcDirs = ['src/androidTest/res']
        }
        // Android instrumentationtest code is in androidInstrumentationTests
        androidTest {
            java.srcDirs = ['src/androidInstrumentationTest/kotlin']
            res.srcDirs = ['src/androidInstrumentationTest/res']
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test'
    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

kotlin {
    jvm()
    android()
    js {
        nodejs {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }

        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}